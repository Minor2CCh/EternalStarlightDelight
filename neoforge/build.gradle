plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        url = "https://cursemaven.com"
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
    implementation("curse.maven:farmers-delight-398521:7596366")
    implementation("curse.maven:eternal-starlight-1080592:7545975")
    // compile against the JEI API but do not include it at runtime
    compileOnly("mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}")
    // at runtime, use the full JEI jar for NeoForge
    runtimeOnly("mezz.jei:jei-${mc_version}-neoforge:${jei_version}")
    //runtimeOnly("curse.maven:jade-324717:6853386") //開発環境だと失敗する
    runtimeOnly("curse.maven:appleskin-248787:6616291")
    runtimeOnly("curse.maven:just-enough-effect-descriptions-jeed-532286:7346553")
    runtimeOnly("curse.maven:food-effect-tooltips-forge-776426:5730649")
    runtimeOnly("curse.maven:cloth-config-348521:5729127")


    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
